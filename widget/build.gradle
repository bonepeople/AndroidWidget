plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    namespace "com.bonepeople.android.widget"
    compileSdk rootProject.androidConfig.compileSdk
    defaultConfig {
        minSdk rootProject.androidConfig.minSdk
        targetSdk rootProject.androidConfig.targetSdk
        buildConfigField("String", "VERSION", """ "${rootProject.androidConfig.versionName}" """)

        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    // Document https://developer.android.com/build/publish-library/configure-pub-variants
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'

    // Kotlin Coroutines https://github.com/Kotlin/kotlinx.coroutines/blob/master/README.md#using-in-your-projects
    // kotlinx-coroutines-android dependency for switching to the Android UI thread
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Local Broadcast https://github.com/bonepeople/LocalBroadcastUtil
    api 'com.github.bonepeople:LocalBroadcastUtil:1.4.1'

    // Gson https://github.com/google/gson
    api 'com.google.code.gson:gson:2.9.1'

    // MMKV https://github.com/tencent/mmkv
    implementation 'com.tencent:mmkv:1.2.14'

    // DataStore https://developer.android.com/topic/libraries/architecture/datastore
    implementation("androidx.datastore:datastore-preferences:1.0.0")

    // AppStartup https://developer.android.google.cn/topic/libraries/app-startup?hl=zh-cn
    implementation("androidx.startup:startup-runtime:1.1.1")
}

// Document https://developer.android.com/build/publish-library/upload-library
publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }
            groupId = rootProject.publishConfig.groupId
            artifactId = rootProject.publishConfig.artifactId
            version = rootProject.publishConfig.publishVersion
        }
    }
}